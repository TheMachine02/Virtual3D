#include	"vxVSL.inc"

lightShader:

	.db	VX_PIXEL_SHADER
	.db	lightEnd-lightCode
	.db	2	; two pixel are written per loop
	.db	11	; total size of per pixel code
.relocate	VX_PIXEL_SHADER_CODE
lightCode:
	ld  a, h    ; get v to pass to next part
	add hl, sp  ; v=v+dv (du on high)
	exx		 ; swap
	ld  h, a    ; paste v on high byte
	ld  e, (hl) ; fetch texture, and get index to color table in same time
	ld  a, (de) ; fetch lighted color
	adc hl, bc  ; u=u+du (on low)
	exx		 ; swap
	ld  (de), a ; write pixel
	inc de      ; next pixel
	ld	a, h
	add	hl, sp
	exx
	ld	h, a
	ld	e, (hl)
	ld	a, (de)
	adc	hl, bc
	exx
	ld	(de), a
	inc	de
	djnz	lightCode
lightEnd:
.endrelocate
